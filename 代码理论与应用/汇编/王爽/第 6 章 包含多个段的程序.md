# 引言

程序获取安全的内存空间有两种方式：
1. 在加载程序的时候，为程序分配
2. 在程序的运行过程中，向操作系统申请

在课程中，我们不讨论第二种方法

如果要一个程序，在加载的时候就获取所需的空间，就必须在源程序中做出说明，否则就只会被分配代码段的内存。

而且为了程序设计上的清晰和方便，我们一般也都定义不同的段来存放它们。

# 6.1 在代码段中使用数据

```
assume cs:code
code segment
	dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h

	mov bx,0
	mov ax,0
    mov cx,8
s:  add ax,cs:[bx]
    add bx,2
    loop s

    mov ax,4c00h
    int 21h
code ends
end
```

- `dw`：define word，定义字类型的数据

因为 `dw` 定义的数据处于代码段的最开始，所以它们的地址为 `cs:0`，`cs:2`，`cs:4`，`cs:4`，...

但是 `cs:ip` 同时也指向了这个地方，如何让它指向指令开始的地方呢？

通过在指令开始的地方添加标记：`start`，然后在结束的关键字后记录这个标记：`end start`。即程序最后的 `end` 关键字不仅标志了程序的结束，还指出了程序开始的地方

程序框架：
```
assume cs:code
code segment
	.
	.
	.
	数据
	.
	.
	.
start:
	.
	.
	.
	代码
	.
	.
	.
code ends
end start
```

# 6.2 在代码段中使用栈

如果我们希望对指定数据倒序排列，如何做的？一个简单的办法，使用栈，但是我们需要提前申请空间，用于栈空间使用

```
assume cs:code
code segment
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    dw 0,0,0,0,0,0,0,0
    
start:  
	mov ax,cs
	mov ss,ax
	mov sp,30h

	mov bx,0
	mov cx,8
s:      
	push cs:[bx]
	add bx,2
	loop s

	mov bx,0
	mov cx,8
s0:
	pop cs:[bx]
	add bx,2
	loop s0

	mov ax,4c00h
	int 21h
code ends
end start
```

使用栈，实际就是申请一块内存，自己当作栈来使用。

# 6.3 将数据、代码、栈放入不同的段

在前面，我们将数据、栈和代码都放到了一个段里面，但是正确的做法应该是放到不同的段里面。(一个段的容量也是有限制的)

```
assume cs:code,ds:data,ss:stack

data segment
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends

stack segment
    dw 0,0,0,0,0,0,0,0
stack ends
    
code segment:
start:  
	mov ax,stack
	mov ss,ax
	mov sp,20h

	mov ax,data
	mov ds,ax

	mov bx,0
	mov cx,8
s:      
	push [bx]
	add bx,2
	loop s

	mov bx,0
	mov cx,8
s0:
	pop [bx]
	add bx,2
	loop s0

	mov ax,4c00h
	int 21h
code ends
end start
```

注意：这些段仅仅是我们自己人为定义的，CPU 并不知道这些。所以还需要给对应寄存器赋值(assume 指令不知道在干什么，就当它不存在吧)。每个段的段名就好像一个立即数，存储了段地址。分段仅仅是让我们提前获知了这个段的段地址立即数。

# 实验 5 编写、调试具有多个段的程序

总结：多个段都是认为定义的，会为其分配以段为单位的内存。段名就是地址。end 可以指出开始执行的代码在哪里。仅此而已了。